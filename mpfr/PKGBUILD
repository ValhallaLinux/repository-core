pkgname=mpfr
_pkgver=3.1.3
_patchlevel=p4
pkgver=${_pkgver}.${_patchlevel}
#pkgver=${_pkgver}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
install=mpfr.install
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz
        mpfr-${_pkgver}-${_patchlevel}.patch)
sha256sums=('6835a08bd992c8257641791e9a6a2b35b02336c8de26d0a8577953747e514a16'
            '3fe8be4aeb8e37784da30d0c8882c4504d16f32bf38ddb49a5535e98d1c165eb')
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')

prepare() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -p1 -i $srcdir/mpfr-${_pkgver}-${_patchlevel}.patch
}

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure \
    --prefix=/usr \
    --enable-thread-safe \
    --enable-shared
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}

